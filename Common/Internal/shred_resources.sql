/*


	Shreds/Extracts the locking XML generated by dbo.list_transactions.



DECLARE @xml xml = N'
<locked_resources>
  <resource resource_type="DATABASE" owning_session_id="63" database="tempdb" resource_subtype="BULKOP_BACKUP_LOG">
    <resource_identifier>DATABASE: 2:0</resource_identifier>
    <transaction request_type="LOCK" request_mode="NULL" request_status="GRANT" reference_count="1" owner_type="TRANSACTION" transaction_id="12899514" />
    <lock_owner_address>0x00000222AEC28D40</lock_owner_address>
  </resource>
  <resource resource_type="OBJECT" owning_session_id="63" database="tempdb" resource_subtype="">
    <resource_identifier>OBJECT: 2:5</resource_identifier>
    <transaction request_type="LOCK" request_mode="IX" request_status="GRANT" reference_count="1" owner_type="TRANSACTION" transaction_id="12899514" />
    <lock_owner_address>0x00000222AEC29640</lock_owner_address>
  </resource>
</locked_resources>';


SELECT * FROM dbo.shred_resources(@xml);


*/

USE [admindb];
GO 


IF OBJECT_ID('dbo.shred_resources','IF') IS NOT NULL
	DROP FUNCTION dbo.shred_resources;
GO

CREATE FUNCTION dbo.shred_resources(@resources xml)
RETURNS TABLE 
AS 
  RETURN	
	
	-- {copyright}

	SELECT 
		[resource].value('resource_identifier[1]', 'sysname') [resource_identifier], 
		[resource].value('@database[1]', 'sysname') [database], 
		[resource].value('(transaction/@transaction_id)[1]', 'bigint') transaction_id,
		[resource].value('(transaction/@request_mode)[1]', 'sysname') lock_mode, 
		[resource].value('(transaction/@reference_count)[1]', 'int') reference_count,
		[resource].value('lock_owner_address[1]', 'sysname') [lock_owner_address], 
		[resource].query('.') [resource_data]
	FROM 
		@resources.nodes('//resource') [XmlData]([resource]);

GO